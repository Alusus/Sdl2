اشمل "مـتم/طـرفية.أسس"؛
اشمل "مـتم/نـظام.أسس"؛
اشمل "مـتم/نـم.أسس"؛
اشمل "مـحا.أسس"؛
مـحا.اشمل_ملف("Alusus/Sdl2", "طـوب.أسس")؛

@تصدير[exit] دالة اخرج (حالة: صحيح);

عرف رسام: مؤشر[طـوب.رسـام]؛

دالة تهيئة {
  إذا طـوب.هيئ(طـوب._تهيئة_عصا_التحكم_) != 0 {
    مـتم.طـرفية.اطبع("فشلت تهئية مكتبة ط.و.ب.\ج")؛
    اخرج(1)؛
  }؛

  إذا طـوب.صـورة.هيئ(طـوب.صـورة._تهيئة_بنج_) != طـوب.صـورة._تهيئة_بنج_ {
    مـتم.طـرفية.اطبع("فشلت تهيئة مكتبة صورة ط.و.ب\ج")؛
    اخرج(1)؛
  }؛

  إذا طـوب.سـمعي.هيئ(44100، طـوب.سـمعي.بـنية._مبدئي_، 2، 2048) < 0 {
    مـتم.طـرفية.اطبع("فشلت تهيئة مكتبة السمعيات\ج")؛
    اخرج(1)؛
  }؛
  
  طـوب.كـتابة.هيئ()؛

  عرف نافذة: مؤشر[طـوب.نـافذة] = طـوب.نـافذة.أنشئ("مطق النار"، 0، 0، 1280، 720، 0)؛
  إذا نافذة == 0 {
    مـتم.طـرفية.اطبع("فشل إنشاء النافذة\ج")؛
    اخرج(1)؛
  }؛

  طـوب.عين_تلميح(طـوب.تـلميحات._رسوم_جودة_التحجيم_، "linear")؛

  رسام = طـوب.رسـام.أنشئ(نافذة، -1، طـوب.رسـام.عـلامات._مسرع_)؛
  إذا رسام == 0 {
    مـتم.طـرفية.اطبع("فشل إنشاء الرسام\ج")؛
    اخرج(1)؛
  }؛
}

عرف نقش: مؤشر[طـوب.نـقش]؛
عرف خط_كبير: مؤشر[طـوب.كـتابة.خـط]؛
عرف خط_صغير: مؤشر[طـوب.كـتابة.خـط]؛
عرف نقش_عنوان_عربي: مؤشر[طـوب.نـقش]؛
عرف نقش_شرح_عربي: مؤشر[طـوب.نـقش]؛
عرف نقش_عنوان_انجليزي: مؤشر[طـوب.نـقش]؛
عرف نقش_شرح_انجليزي: مؤشر[طـوب.نـقش]؛
عرف أبعاد_العنوان_العربي: طـوب.مـستطيل؛
عرف أبعاد_الشرح_العربي: طـوب.مـستطيل؛
عرف أبعاد_العنوان_ألانجليزي: طـوب.مـستطيل؛
عرف موسيقى: مؤشر[طـوب.سـمعي.مـوسيقى]؛
عرف صوت: مؤشر[طـوب.سـمعي.صـوت]؛

دالة جهز_الموارد {
  نقش = طـوب.صـورة.حمل_نقش(رسام، "player.png")؛

  // هيئ الكتابة
  خط_كبير = طـوب.كـتابة.افتح_خط("AlususMono.otf"، 40)؛
  إذا خط_كبير == 0 {
    مـتم.طـرفية.اطبع("فشل تحميل الخط.\ج")؛
    اخرج(1)؛
  }؛
  خط_صغير = طـوب.كـتابة.افتح_خط("AlususMono.otf"، 20)؛
  إذا خط_صغير == 0 {
    مـتم.طـرفية.اطبع("فشل تحميل الخط.\ج")؛
    اخرج(1)؛
  }؛

  عرف لون: طـوب.لـون؛
  
  لون.ح = 0؛ لون.خ = 255؛ لون.ز = 0؛ لون.ش = 255؛
  نقش_عنوان_عربي = طـوب.كـتابة.أنشئ_نقش_كتابة("اخْتِبارُ الرُسوم"، لون~مؤشر، خط_كبير، 1، رسام)؛
  لون.ح = 0؛ لون.خ = 0؛ لون.ز = 0؛ لون.ش = 255؛
  نقش_شرح_عربي = طـوب.كـتابة.أنشئ_نقش_كتابة("اضْغَط مِفْتاحَ المَسافَةِ لِلمُؤَثِّراتِ الصَوْتِيّة", لون~مؤشر, خط_صغير, 1, رسام)؛

  لون.ح = 0؛ لون.خ = 0؛ لون.ز = 255؛ لون.ش = 255؛
  نقش_عنوان_انجليزي = طـوب.كـتابة.أنشئ_نقش_كتابة("Ship"، لون~مؤشر، خط_كبير، 0، رسام)؛  
  لون.ح = 0؛ لون.خ = 0؛ لون.ز = 0؛ لون.ش = 255؛
  نقش_شرح_انجليزي = طـوب.كـتابة.أنشئ_نقش_كتابة("Press Space for sound effects", لون~مؤشر, خط_صغير, 0, رسام);

  طـوب.نـقش.استفهم(نقش_عنوان_عربي، 0، 0، أبعاد_العنوان_العربي.ع~مؤشر، أبعاد_العنوان_العربي.ط~مؤشر)؛
  طـوب.نـقش.استفهم(نقش_شرح_عربي، 0، 0، أبعاد_الشرح_العربي.ع~مؤشر، أبعاد_الشرح_العربي.ط~مؤشر)؛
  طـوب.نـقش.استفهم(نقش_عنوان_انجليزي، 0، 0، أبعاد_العنوان_ألانجليزي.ع~مؤشر، أبعاد_العنوان_ألانجليزي.ط~مؤشر)؛  

  موسيقى = طـوب.سـمعي.حمل_موسيقى("BoxCat_Games_-_07_-_Inspiration.mp3");
  إذا موسيقى == 0 {
    مـتم.طـرفية.اطبع("فشل تحميل الموسيقى\ج")؛
    اخرج(1)؛
  }؛

  صوت = طـوب.سـمعي.حمل_صوت("bullet_whizzing_by-Mike_Koenig.wav");
  إذا صوت == 0 {
    مـتم.طـرفية.اطبع("فشل تحميل الصوت\ج")؛
    اخرج(1)؛
  }
}؛

دالة اطمغ (نقش: مؤشر[طـوب.نـقش]، س: صحيح، ص: صحيح) {
  عرف مستطيل: طـوب.مـستطيل؛
  مستطيل.س = س؛
  مستطيل.ص = ص؛
  طـوب.نـقش.استفهم(نقش، 0، 0، مستطيل.ع~مؤشر، مستطيل.ط~مؤشر)؛
  طـوب.رسـام.انسخ(رسام، نقش، 0، مستطيل~مؤشر)؛
}؛

دالة ابدأ {
  تهيئة()؛
  جهز_الموارد()؛

  طـوب.سـمعي.شغل(موسيقى، 1)؛

  عرف س: مصفوفة[صحيح، 100]؛
  عرف ص: مصفوفة[صحيح، 100]؛
  س(0) = 0؛
  ص(0) = 100؛
  عرف م: صحيح؛

  بينما 1 {
    طـوب.رسـام.عين_لون_الرسم(رسام، 96، 128، 255، 255)؛
    طـوب.رسـام.نظف(رسام)؛

    لكل م = 99، م > 0، --م {
      س(م) = س(م - 1)؛
      ص(م) = ص(م - 1)؛
    }

    لكل م = 9، م >= 0، --م {
      اطمغ(نقش، س(م * 10)، ص(م * 10))؛
    }؛

    اطمغ(نقش_عنوان_عربي، 1280 - أبعاد_العنوان_العربي.ع، 0)؛
    اطمغ(نقش_شرح_عربي، 1280 - أبعاد_الشرح_العربي.ع، أبعاد_العنوان_العربي.ط)؛
    اطمغ(نقش_عنوان_انجليزي، 0، 0)؛
    اطمغ(نقش_شرح_انجليزي، 0، أبعاد_العنوان_ألانجليزي.ط)؛

    عرف حدث: طـوب.حـدث؛
    بينما طـوب.استشعر_حدث(حدث~مؤشر) != 0 {
      إذا حدث.نوع == طـوب.حـدث._زر_للأسفل_ {
        إذا حدث~مؤشر~مثل[مؤشر[طـوب.حـدث_مفاتيح]]~محتوى.كود_المسح == طـوب.لـوح_المفاتيح.كـود_المسح.ESCAPE اخرج(0)؛
        إذا حدث~مؤشر~مثل[مؤشر[طـوب.حـدث_مفاتيح]]~محتوى.كود_المسح == طـوب.لـوح_المفاتيح.كـود_المسح.SPACE {
          طـوب.سـمعي.شغل(-1، صوت، 0، -1)؛
        }؛
      }
      إذا حدث.نوع == طـوب.حـدث._خروج_ اخرج(0)؛
    }؛

    عرف حالة_المفاتيح: مؤشر[مصفوفة[طبيعي[8]]] = طـوب.لـوح_المفاتيح.هات_الحالة(0)؛
    إذا حالة_المفاتيح~محتوى(طـوب.لـوح_المفاتيح.كـود_المسح.RIGHT) != 0 { ++س(0) }؛
    إذا حالة_المفاتيح~محتوى(طـوب.لـوح_المفاتيح.كـود_المسح.LEFT) != 0 { --س(0) }؛
    إذا حالة_المفاتيح~محتوى(طـوب.لـوح_المفاتيح.كـود_المسح.DOWN) != 0 { ++ص(0) }؛
    إذا حالة_المفاتيح~محتوى(طـوب.لـوح_المفاتيح.كـود_المسح.UP) != 0 { --ص(0) }؛

    طـوب.رسـام.اعرض(رسام)؛

    مـتم.نـظام.نم(1000)؛
  };
}؛

ابدأ()؛

